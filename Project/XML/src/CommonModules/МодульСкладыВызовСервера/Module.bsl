// @strict-types

#Область СоздатьПустойСкладUseCase

// Создать пустой склад сервер.
// 
// Параметры:
//  Запрос - Структура:
// * НаименованиеСклада - Строка
// * АдресСклада - Строка
// 
// Возвращаемое значение:
//  Структура - Ответ создать пустой склад:
// * СкладСсылка - СправочникСсылка.Склады  
Функция СоздатьПустойСкладСервер(Запрос) Экспорт
	НаименованиеСклада = Запрос.НаименованиеСклада;
	АдресСклада = Запрос.АдресСклада;
	Если ПустаяСтрока(НаименованиеСклада) Тогда
		ВызватьИсключениеПустоеНазваниеСклада();
	КонецЕсли;
	Если ПустаяСтрока(АдресСклада) Тогда
		ВызватьИсключениеПустойАдресСклада();
	КонецЕсли;
	ПроверитьСкладНаДубликат(НаименованиеСклада, АдресСклада);
	Склад = СоздатьОбъектПустойСклад(НаименованиеСклада, АдресСклада);
	Склад.Записать();
	Возврат ОтветСоздатьПустойСклад(Склад);
КонецФункции

// Ответ создать пустой склад.
// 
// Параметры:
//  Склад - СправочникОбъект.Склады
// 
// Возвращаемое значение:
//  Структура - Ответ создать пустой склад:
// * СкладСсылка - СправочникСсылка.Склады
Функция ОтветСоздатьПустойСклад(Склад)
	Ответ = Новый Структура;
	Ответ.Вставить("СкладСсылка", Склад.Ссылка);
	Возврат Ответ;
КонецФункции

// Проверить склад на дубликат.
// 
// Параметры:
//  НаименованиеСклада - Строка
//  АдресСклада - Строка
Процедура ПроверитьСкладНаДубликат(НаименованиеСклада, АдресСклада)	
	ЗапросВБазу = Новый Запрос;
	ЗапросВБазу.Текст = 
	"ВЫБРАТЬ 
	|Склады.Наименование КАК Наименование,
	|Склады.Адрес КАК Адрес
	|ИЗ Справочник.Склады КАК Склады
	|ГДЕ Склады.Наименование = &Наименование И Склады.Адрес = &Адрес";
	ЗапросВБазу.УстановитьПараметр("Наименование", НаименованиеСклада);
	ЗапросВБазу.УстановитьПараметр("Адрес", АдресСклада);
	РезультатЗапроса = ЗапросВБазу.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		ВызватьИсключениеДубликатСкладаПоИмениИАдресу(НаименованиеСклада, АдресСклада);
	КонецЕсли;
КонецПроцедуры

// Вызвать исключение дубликат склада по имени и адресу.
// 
// Параметры:
//  НаименованиеСклада - Строка
//  АдресСклада - Строка
Процедура ВызватьИсключениеДубликатСкладаПоИмениИАдресу(НаименованиеСклада, АдресСклада)
	Ошибка = СтрШаблон("Склад с названием %1 и адресом %2 уже существует.", НаименованиеСклада, АдресСклада);
	ВызватьИсключение Ошибка;	
КонецПроцедуры

Процедура ВызватьИсключениеПустоеНазваниеСклада()
	Ошибка = "Название склада не было указано.";
	ВызватьИсключение(Ошибка);
КонецПроцедуры

Процедура ВызватьИсключениеПустойАдресСклада()
	Ошибка = "Адрес склада был пустым";
	ВызватьИсключение(Ошибка);
КонецПроцедуры

// Создать объект пустой склад.
// 
// Параметры:
//  НаименованиеСклада - Строка
//  АдресСклада - Строка
// 
// Возвращаемое значение:
//  СправочникОбъект.Склады - Создать объект пустой склад
Функция СоздатьОбъектПустойСклад(НаименованиеСклада, АдресСклада)
	Склад = Справочники.Склады.СоздатьЭлемент();
	Склад.Код = МодульПримитивовКлиентСервер.СгенерироватьGuid();
	Склад.Наименование = НаименованиеСклада;
	Склад.Адрес = АдресСклада;
	Склад.КоличествоТовара = 0;
	Возврат Склад;
КонецФункции

#КонецОбласти